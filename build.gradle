plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'checkstyle'
}

group = 'eu.senla'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    lombokVersion = '1.18.34'
    seleniumVersion = '4.34.0'
    testngVersion = '7.9.0'
}

dependencies {
    implementation "org.testng:testng:${testngVersion}"
    implementation 'com.github.javafaker:javafaker:1.0.2'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.16'
    implementation "org.seleniumhq.selenium:selenium-java:${seleniumVersion}"
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.16'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
}

spotless {
    enforceCheck = false
    java {
        //means that Spotless is trying to compare your code against the Git reference origin/master
        //ratchetFrom 'origin/master'
        target 'src/**/*.java'
        removeUnusedImports()
        endWithNewline()
        // palantirJavaFormat('2.47.0')
        trimTrailingWhitespace()
        indentWithSpaces(4)
        importOrder('', 'java', 'javax', '\\#')
    }
}

checkstyle {
    toolVersion = '10.12.1'
    configFile = file('config/checkstyle/checkstyle.xml')
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required = false
        html.required = true
        html.stylesheet = resources.text.fromFile('config/xsl/checkstyle-custom.xsl')
    }
}

test {
    useTestNG() {
        suites "src/test/resources/config/${config}.xml"
    }

    systemProperty "browser", project.findProperty("browser")
    systemProperty "threadCount", project.findProperty("threadCount") ?: "1"

    options {
        listeners.add("eu.senla.listeners.SuiteXmlTransformer")
    }
}